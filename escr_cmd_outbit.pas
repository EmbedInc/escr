{   Subroutine ESCR_CMD_OUTBIT (BUF, SP, STAT)
*
*   /OUTBIT name port bit [polarity] [ival]
*
*   Declares a particular I/O bit and indicates that the I/O bit will be
*   used as an output.  PORT is the name of the port containing the bit,
*   BIT is the bit number within the register, and IVAL is the initial
*   value to set the bit to.  The default for IVAL is 0.  The following
*   assembler constants will be declared:
*
*     <name>_reg   -  Address of port register containing I/O bit.
*     <name>_tris  -  Address of TRIS register controlling in/out direction.
*     <name>_bit   -  Number of bit within port and tris regs for this I/O bit.
*     <name>_lat   -  Address of LAT register for this port, if any.
*
*   The following assembler variables will be updated:
*
*     VAL_PORTx  -  Initial value for PORT register
*     VAL_TRISx  -  Initial value for TRIS register
*
*   String substitution macros will be defined:
*
*     #define <name>_pin <port>,<bit>
*     #define <name>_pinlat <lat>,<bit>
}
module escr_cmd_outbit;
define escr_cmd_outbit;
%include '/cognivision_links/dsee_libs/pic/escr.ins.pas';

procedure escr_cmd_outbit (
  out     stat: sys_err_t);
  val_param;

const
  max_msg_parms = 1;                   {max parameters we can pass to a message}

var
  bit: sys_int_machine_t;              {number of I/O bit within port register}
  ival: sys_int_machine_t;             {0 or 1 initial value of the I/O bit}
  im: sys_int_max_t;                   {max size integer}
  pick: sys_int_machine_t;             {number of keyword picked from list}
  positive: boolean;                   {pin polarity is positive, ON = high}
  portl: char;                         {A-Z I/O port name, lower case}
  portu: char;                         {A-Z I/O port name, upper case}
  name: string_var80_t;                {I/O bit name}
  namel: string_var80_t;               {lower case I/O bit name}
  strbit: string_var16_t;              {decimal integer I/O bit number string}
  tk: string_var80_t;                  {scratch token}
  syname: string_var32_t;              {scratch symbol name}
  sym_p: sym_p_t;                      {pointer to newly created symbol}
  msg_parm:                            {parameter references for messages}
    array[1..max_msg_parms] of sys_parm_msg_t;

label
 have_ivaltk, end_parms, done_parms;

begin
  if e.inhibit_p^.inh then return;     {execution is inhibited ?}
  name.max := size_char(name.str);     {init local var strings}
  namel.max := size_char(namel.str);
  strbit.max := size_char(strbit.str);
  tk.max := size_char(tk.str);
  syname.max := size_char(syname.str);

  positive := true;                    {init optional parameters to defaults}
  ival := 0;

  if not get_token (name)              {get NAME parameter}
    then escr_err_parm_missing ('', '', nil, 0);
  string_copy (name, namel);           {make lower case version of name}
  string_downcase (namel);
{
*   Get PORTx parameter.
}
  if not get_token (tk)                {get I/O port name into TK}
    then escr_err_parm_missing ('', '', nil, 0);
  string_downcase (tk);                {make lower case port register name}
  portl := tk.str[5];                  {extract lower case a-z port letter}
  if                                   {invalid port register name ?}
      (tk.len <> 5) or                 {not right length for "PORTx"}
      (tk.str[1] <> 'p') or            {does not start with "port"}
      (tk.str[2] <> 'o') or
      (tk.str[3] <> 'r') or
      (tk.str[4] <> 't') or
      (portl < 'a') or (portl > 'z')   {not a valid port letter ?}
      then begin
    escr_err_parm_last_bad;
    end;
  portu := string_upcase_char (portl); {make upper case port letter}
{
*   Get bit number parameter.
}
  if not get_int (im) then begin       {get the bit number}
    escr_err_parm_missing ('', '', nil, 0);
    end;
  bit := im;                           {get the bit number into BIT}
  if bit < 0 then escr_err_parm_last_bad; {negative bit numbers not allowed}
  case lang of
lang_aspic_k: begin                    {MPASM}
      if bit > 7 then escr_err_parm_last_bad; {bit value too large ?}
      end;
lang_dspic_k: begin                    {ASM30}
      if bit > 15 then escr_err_parm_last_bad; {bit value too large ?}
      end;
otherwise
    err_lang (lang, 'ESCR_CMD_INBIT', 1);
    end;
  string_f_int (strbit, bit);          {make bit number string}
{
*   Process the optional parameters.
}
  if not get_token (tk) then goto done_parms; {get polarity or initial value token}
  string_upcase (tk);                  {make upper case for keyword mathing}
  string_tkpick80 (tk, 'P N', pick);   {check for polarity keyword}
  case pick of                         {which keyword is it}
1:  begin                              {P}
      positive := true;
      ival := 0;
      end;
2:  begin                              {N}
      positive := false;
      ival := 1;
      end;
otherwise                              {not a polarity keyword}
    goto have_ivaltk;                  {re-use TK for initial value parameter}
    end;

  if not get_token (tk) then goto done_parms; {get initial value token}
  string_upcase (tk);                  {make upper case for keyword matching}
have_ivaltk:                           {initial value token is in TK}
  string_tkpick80 (tk, 'ON OFF', pick); {check for initial value keywords}
  case pick of                         {which keyword is it}
1:  begin                              {ON}
      if positive
        then ival := 1
        else ival := 0;
      goto end_parms;
      end;
2:  begin                              {OFF}
      if positive
        then ival := 0
        else ival := 1;
      goto end_parms;
      end;
otherwise                              {not one of the valid keywords}
    string_t_int (tk, ival, stat);     {try to convert to integer}
    if sys_error(stat) then escr_err_parm_last_bad; {not a integer value}
    if (ival < 0) or (ival > 1) then escr_err_parm_last_bad; {value is out of range ?}
    end;

end_parms:                             {no more command parameters allowed}
  escr_get_end;

done_parms:                            {done reading command parameters}
{
*   Create the preprocessor constant Portdata_<port><bit> where PORT is the
*   single letter port name and BIT is the 0-N number of this bit within the
*   port.
}
  syname.len := 0;                     {build the constant name}
  string_appends (syname, 'Portdata_'(0));
  string_append1 (syname, portl);
  string_append (syname, strbit);

  escr_sym_find (syname, sym_p);       {find constant if it already exists}
  if sym_p <> nil then begin           {already exists ?}
    escr_sym_del (sym_p);              {delete it}
    end;

  tk.len := 0;                         {build the constant string value}
  string_append_token (tk, name);      {add name of this I/O pin}
  string_append_token (tk, string_v('OUT'(0))); {this is a output bit}
  if positive
    then string_append_token (tk, string_v('POS'(0)))
    else string_append_token (tk, string_v('NEG'(0)));

  escr_sym_new_const (                 {create the constant}
    syname,                            {name of the constant}
    dtype_str_k,                       {value will be a string}
    tk.len,                            {string length}
    true,                              {make this new symbol global}
    sym_p);                            {returned pointer to the new symbol}
  string_copy (tk, sym_p^.const_val.str); {set the constant's value}
{
*   Create the preprocessor constant
*
*     Outbit_<name>_port
*
*   This is a string constant that contains the upper case name of the port.
*   For example, if the bit is within Port B, then the value of this constant
*   would be "B".
}
  syname.len := 0;                     {build the constant name}
  string_appends (syname, 'Outbit_'(0));
  string_append (syname, namel);
  string_appends (syname, '_port'(0));

  escr_sym_find (syname, sym_p);       {find constant if it already exists}
  if sym_p <> nil then begin           {already exists ?}
    sys_msg_parm_vstr (msg_parm[1], name);
    escr_err_atline ('pic', 'err_outbit_dup', msg_parm, 1); {bomb with error message}
    end;

  tk.len := 0;                         {build the string value}
  string_append1 (tk, portu);

  escr_sym_new_const (                 {create the constant}
    syname,                            {name of the constant}
    dtype_str_k,                       {value will be a string}
    tk.len,                            {string length}
    true,                              {make this new symbol global}
    sym_p);                            {returned pointer to the new symbol}
  string_copy (tk, sym_p^.const_val.str); {set the constant's value}
{
*   Create the preprocessor constant
*
*     Outbit_<name>_bit
*
*   This is a integer constant set to the bit number of this outbit within its
*   port register.
}
  syname.len := 0;                     {build the constant name}
  string_appends (syname, 'Outbit_'(0));
  string_append (syname, namel);
  string_appends (syname, '_bit'(0));

  escr_sym_find (syname, sym_p);       {find constant if it already exists}
  if sym_p <> nil then begin           {already exists ?}
    sys_msg_parm_vstr (msg_parm[1], name);
    escr_err_atline ('pic', 'err_outbit_dup', msg_parm, 1); {bomb with error message}
    end;

  escr_sym_new_const (                 {create the constant}
    syname,                            {name of the constant}
    dtype_int_k,                       {value will be integer}
    0,                                 {unused for integer data type}
    true,                              {make this new symbol global}
    sym_p);                            {returned pointer to the new symbol}

  sym_p^.const_val.int := bit;         {set the value of the new constant}

  case lang of                         {what is the input source language ?}
{
********************
*
*   Input source language is MPASM.
}
lang_aspic_k: begin
{
*   <name>_reg equ portx
}
  string_append (e.obuf, name);
  string_appends (e.obuf, '_reg equ port'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;
{
*     ifdef trisx
*   <name>_tris equ trisx
*       endif
}
  string_appends (e.obuf, '  ifdef tris'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;

  string_append (e.obuf, name);
  string_appends (e.obuf, '_tris equ tris'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;

  string_appends (e.obuf, '    endif'(0));
  escr_write_obuf;
{
*   <name>_bit equ <bit>
}
  string_append (e.obuf, name);
  string_appends (e.obuf, '_bit equ '(0));
  string_append (e.obuf, strbit);
  escr_write_obuf;
{
*   val_trisx set val_trisx & ~(1 << <bit>)
}
  string_appends (e.obuf, 'val_tris'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ' set val_tris'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ' & ~(1 << '(0));
  string_append (e.obuf, strbit);
  string_appends (e.obuf, ')'(0));
  escr_write_obuf;
{
*   Update VAL_PORTx according to the IVAL parameter.
*
*   For 0: val_portx set val_portx & ~(1 << <bit>)
*   For 1: val_portx set val_portx | (1 << <bit>)
}
  string_appends (e.obuf, 'val_port'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ' set val_port'(0));
  string_append1 (e.obuf, portl);
  if ival = 0
    then string_appends (e.obuf, ' & ~(1 << '(0)) {initial value is 0}
    else string_appends (e.obuf, ' | (1 << '(0)); {initial value is 1}
  string_append (e.obuf, strbit);
  string_appends (e.obuf, ')'(0));
  escr_write_obuf;
{
*   #define <name>_pin portx,<bit>
}
  string_appends (e.obuf, '#define '(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_pin port'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ',');
  string_append (e.obuf, strbit);
  escr_write_obuf;
{
*     ifdef latx
*   <name>_lat equ latx
*   #define <name>_pinlat latx,<bit>
*       endif
}
  string_appends (e.obuf, '  ifdef lat'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;

  string_append (e.obuf, name);
  string_appends (e.obuf, '_lat equ lat'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;

  string_appends (e.obuf, '#define '(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_pinlat lat'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ','(0));
  string_append (e.obuf, strbit);
  escr_write_obuf;

  string_appends (e.obuf, '    endif'(0));
  escr_write_obuf;
{
*     ifdef latx
*   set_<name>_off macro
*         dbankif latx
*         bxf latx,<bit>
*         endm
*   set_<name>_on macro
*         dbankif latx
*         bxf latx,<bit>
*         endm
*       endm
}
  string_appends (e.obuf, '  ifdef lat'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;

  string_appends (e.obuf, 'set_'(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_off macro'(0));
  escr_write_obuf;

  string_appends (e.obuf, '      dbankif lat'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;

  string_appends (e.obuf, '      b'(0));
  if positive
    then string_append1 (e.obuf, 'c')
    else string_append1 (e.obuf, 's');
  string_appends (e.obuf, 'f lat'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ','(0));
  string_append (e.obuf, strbit);
  escr_write_obuf;

  string_appends (e.obuf, '      endm'(0));
  escr_write_obuf;

  string_appends (e.obuf, 'set_'(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_on macro'(0));
  escr_write_obuf;

  string_appends (e.obuf, '      dbankif lat'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;

  string_appends (e.obuf, '      b'(0));
  if positive
    then string_append1 (e.obuf, 's')
    else string_append1 (e.obuf, 'c');
  string_appends (e.obuf, 'f lat'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ','(0));
  string_append (e.obuf, strbit);
  escr_write_obuf;

  string_appends (e.obuf, '      endm'(0));
  escr_write_obuf;
{
*       else
*   set_<name>_off macro
*         dbankif portx
*         bxf portx,<bit>
*         endm
*   set_<name>_on macro
*         dbankif portx
*         bxf portx,<bit>
*         endm
*       endif
}
  string_appends (e.obuf, '    else'(0));
  escr_write_obuf;

  string_appends (e.obuf, 'set_'(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_off macro'(0));
  escr_write_obuf;

  string_appends (e.obuf, '      dbankif port'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;

  string_appends (e.obuf, '      b'(0));
  if positive
    then string_append1 (e.obuf, 'c')
    else string_append1 (e.obuf, 's');
  string_appends (e.obuf, 'f port'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ','(0));
  string_append (e.obuf, strbit);
  escr_write_obuf;

  string_appends (e.obuf, '      endm'(0));
  escr_write_obuf;

  string_appends (e.obuf, 'set_'(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_on macro'(0));
  escr_write_obuf;

  string_appends (e.obuf, '      dbankif port'(0));
  string_append1 (e.obuf, portl);
  escr_write_obuf;

  string_appends (e.obuf, '      b'(0));
  if positive
    then string_append1 (e.obuf, 's')
    else string_append1 (e.obuf, 'c');
  string_appends (e.obuf, 'f port'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ','(0));
  string_append (e.obuf, strbit);
  escr_write_obuf;

  string_appends (e.obuf, '      endm'(0));
  escr_write_obuf;

  string_appends (e.obuf, '    endif'(0));
  escr_write_obuf;

  end;                                 {end of MPASM language case}
{
********************
*
*   Input source language is MPASM.
}
lang_dspic_k: begin
{
*   .equ <name>_reg, _PORTx
}
  string_appends (e.obuf, '.equ '(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_reg, _PORT'(0));
  string_append1 (e.obuf, portu);
  escr_write_obuf;
{
*   .equ <name>_tris, _TRISx
}
  string_appends (e.obuf, '.equ '(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_tris, _TRIS'(0));
  string_append1 (e.obuf, portu);
  escr_write_obuf;
{
*   .equ <name>_bit, <bit>
}
  string_appends (e.obuf, '.equ '(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_bit, '(0));
  string_append (e.obuf, strbit);
  escr_write_obuf;
{
*   .equ <name>_lat, _LATx
}
  string_appends (e.obuf, '.equ '(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_lat, _LAT'(0));
  string_append1 (e.obuf, portu);
  escr_write_obuf;
{
*   .macro set_<name>_on
*     (bset or bclr) _LATx, #<bit>
*   .endm
}
  string_appends (e.obuf, '.macro set_'(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_on'(0));
  escr_write_obuf;
  string_appends (e.obuf, '  '(0));
  if positive
    then string_appends (e.obuf, 'bset'(0))
    else string_appends (e.obuf, 'bclr'(0));
  string_appends (e.obuf, ' _LAT'(0));
  string_append1 (e.obuf, portu);
  string_appends (e.obuf, ', #'(0));
  string_append (e.obuf, strbit);
  escr_write_obuf;
  string_appends (e.obuf, '  .endm'(0));
  escr_write_obuf;
{
*   .macro set_<name>_off
*     (bclr or bset) _LATx, #<bit>
*   .endm
}
  string_appends (e.obuf, '.macro set_'(0));
  string_append (e.obuf, name);
  string_appends (e.obuf, '_off'(0));
  escr_write_obuf;
  string_appends (e.obuf, '  '(0));
  if positive
    then string_appends (e.obuf, 'bclr'(0))
    else string_appends (e.obuf, 'bset'(0));
  string_appends (e.obuf, ' _LAT'(0));
  string_append1 (e.obuf, portu);
  string_appends (e.obuf, ', #'(0));
  string_append (e.obuf, strbit);
  escr_write_obuf;
  string_appends (e.obuf, '  .endm'(0));
  escr_write_obuf;
{
*   Update VAL_PORTx according to the IVAL parameter.
*
*   For 0: .set val_portx, val_portx & ~(1 << <bit>)
*   For 1: .set val_portx, val_portx | (1 << <bit>)
}
  string_appends (e.obuf, '.set val_port'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ', val_port'(0));
  string_append1 (e.obuf, portl);
  if ival = 0
    then string_appends (e.obuf, ' & ~(1 << '(0)) {initial value is 0}
    else string_appends (e.obuf, ' | (1 << '(0)); {initial value is 1}
  string_append (e.obuf, strbit);
  string_appends (e.obuf, ')'(0));
  escr_write_obuf;
{
*   .set val_trisx, val_trisx & ~(1 << <bit>)
}
  string_appends (e.obuf, '.set val_tris'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ', val_tris'(0));
  string_append1 (e.obuf, portl);
  string_appends (e.obuf, ' & ~(1 << '(0));
  string_append (e.obuf, strbit);
  string_appends (e.obuf, ')'(0));
  escr_write_obuf;

  end;                                 {end of ASM30 language case}
{
********************
*
*   Unexpected input source file language.
}
otherwise
    err_lang (lang, 'ESCR_CMD_OUTBIT', 2);
    end;
  end;
